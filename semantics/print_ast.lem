open MiniML

val Num : Int.int -> num
let inline {hol} Num = Hol.Num

val CHR : num -> string
let inline {hol} CHR n = Hol.STRING (Hol.CHR n) ""

val string_first : string -> string
let inline {hol} string_first s = Hol.STRING (Hol.SUB (s,0)) ""

val first_ord : string -> num
let inline {hol} first_ord s = Hol.ORD (Hol.SUB (s,0))

val (%) : num -> num -> num
let inline {hol} (%) = Hol.MOD

type token =
| WhitespaceT of num
| NewlineT 
| HashT | LparT | RparT | StarT | CommaT | ArrowT | DotsT | ColonT | SealT 
| SemicolonT | EqualsT | DarrowT | LbrackT | RbrackT | UnderbarT | LbraceT 
| BarT | RbraceT | AbstypeT | AndT | AndalsoT | AsT | CaseT | DatatypeT | DoT 
| ElseT | EndT | EqtypeT | ExceptionT | FnT | FunT | FunctorT | HandleT | IfT 
| InT | IncludeT | InfixT | InfixrT | LetT | LocalT | NonfixT | OfT | OpT 
| OpenT | OrelseT | RaiseT | RecT | SharingT | SigT | SignatureT | StructT 
| StructureT | ThenT | TypeT | ValT | WhereT | WhileT | WithT | WithtypeT 
| ZeroT 
| DigitT of string
| NumericT of string
| IntT of int
| HexintT of string
| WordT of string
| HexwordT of string
| RealT of string
| StringT of string
| CharT of string
| TyvarT of string
| AlphaT of string
| SymbolT of string
| LongidT of string

(* OCaml additions *)
  | MatchT | AmpampT | BarbarT | SemisemiT

let rec num_to_string n acc =
  if n = 0 then
    acc
  else
    num_to_string (n / 10) (CHR (n % 10 + 48) ^ acc)

val int_to_string : bool -> Int.int -> string
let int_to_string sml n =
  if n = i 0 then
    "0"
  else if Int.(>) n (i 0) then
    num_to_string (Num n) ""
  else
    (if sml then "~" else "-") ^ num_to_string (Num (Int.(-) (i 0) n)) ""

val spaces : num -> string -> string
let rec 
spaces n s =
  if (n:num) = 0 then
    s
  else
    " " ^ spaces (n - 1) s

let space_append s1 s2 =
  if s2 = "" then
    s1
  else 
    let f = string_first s2 in
      if f = ")"  || f = " " || f = "," then
    s1 ^ s2
  else
    s1 ^ " " ^ s2

let rec
tok_to_string sml NewlineT s = "\n" ^ s
and
tok_to_string sml (WhitespaceT n) s = spaces n s
and
tok_to_string sml (IntT i) s = space_append (int_to_string sml i) s
and
tok_to_string sml (LongidT id) s = space_append id s
and
tok_to_string sml (TyvarT tv) s = space_append ("'" ^ tv) s
and
tok_to_string sml AndT s = "and " ^ s
and
tok_to_string sml AndalsoT s = "andalso " ^ s
and
tok_to_string sml CaseT s = "case " ^ s
and
tok_to_string sml DatatypeT s = "datatype " ^ s
and
tok_to_string sml ElseT s = "else " ^ s
and
tok_to_string sml EndT s = "end " ^ s
and
tok_to_string sml FnT s = "fn " ^ s
and
tok_to_string sml FunT s = "fun " ^ s
and
tok_to_string sml IfT s = "if " ^ s
and
tok_to_string sml InT s = "in " ^ s
and
tok_to_string sml LetT s = "let " ^ s
and
tok_to_string sml OfT s = "of " ^ s
and
tok_to_string sml OpT s = "op " ^ s
and
tok_to_string sml OrelseT s = "orelse " ^ s
and
tok_to_string sml RecT s = "rec " ^ s
and
tok_to_string sml ThenT s = "then " ^ s
and
tok_to_string sml ValT s = "val " ^ s
and
tok_to_string sml LparT s = 
  if s = "" then
    "("
  else if string_first s = "*" then
    "( " ^ s
  else
    "(" ^ s
and
tok_to_string sml RparT s = space_append ")" s
and
tok_to_string sml CommaT s = ", " ^ s
and
tok_to_string sml SemicolonT s = ";" ^ s
and
tok_to_string sml BarT s = "| " ^ s
and
tok_to_string sml EqualsT s = "= " ^ s
and
tok_to_string sml DarrowT s = "=> " ^ s
and
tok_to_string sml ArrowT s = "-> " ^ s
and
tok_to_string sml StarT s = "* " ^ s
and
tok_to_string sml MatchT s = "match " ^ s
and
tok_to_string sml TypeT s = "type " ^ s
and
tok_to_string sml WithT s = "with " ^ s
and
tok_to_string sml AmpampT s = "&& " ^ s
and
tok_to_string sml BarbarT s = "|| " ^ s
and
tok_to_string sml SemisemiT s = ";;" ^ s

let rec 
tok_list_to_string sml [] = ""
and
tok_list_to_string sml (t::l) = 
  tok_to_string sml t (tok_list_to_string sml l)

(*type 'a tree = L of 'a | N of 'a tree * 'a tree*)
type tok_tree = L of token | N of tok_tree * tok_tree

(*val (^^) : forall 'a. 'a tree -> 'a tree -> 'a tree*)
val (^^) : tok_tree -> tok_tree -> tok_tree
let inline {hol} (^^) x y = N x y

(*val tree_to_list : forall 'a. 'a tree -> 'a list -> 'a list*)
val tree_to_list : tok_tree -> list token -> list token
let rec
tree_to_list (L x) acc = x::acc
and
tree_to_list (N x1 x2) acc = tree_to_list x1 (tree_to_list x2 acc)

(* Should include "^", but I don't know how to get that into HOL, since
 * antiquote seem stronger than strings.  See the specification in
 * print_astProofsScript. *)
let is_sml_infix s =
  let c = first_ord s in
    if c < 65 (* "A" *) then
      if c < 60 (* "<" *) then
        s = "*" ||
        s = "+" || 
        s = "-" ||
        s = "/" ||
        s = "::" || 
        s = ":="
      else
        s = "<" || 
        s = "<=" || 
        s = "<>" ||
        s = "=" || 
        s = ">" || 
        s = ">=" || 
        s = "@"
    else
      if c < 109 (* "m" *) then
        if c < 100 then
          s = "before"
        else
          s = "div" 
      else
        if c < 111 then
          s = "mod"
        else
          s = "o"

let is_ocaml_infix s =
  let c = first_ord s in
    if c < 65 then
      List.mem s ["*"; "+"; "-"; "/"; "<"; "<="; "="; ">"; ">="]
    else 
      s = "mod"

(*val join_trees : forall 'a. 'a tree -> 'a tree list -> 'a tree*)
val join_trees : tok_tree -> list tok_tree -> tok_tree
let rec
join_trees sep [x] = x
and
join_trees sep (x::y::l) =
  x ^^ sep ^^ join_trees sep (y::l)

let rec
lit_to_tok_tree sml (Bool true) = L (LongidT "true")
and
lit_to_tok_tree sml (Bool false) = L (LongidT "false")
and
lit_to_tok_tree sml (IntLit n) = L (IntT n)

let var_to_tok_tree sml v =
  if sml && is_sml_infix v then
    L OpT ^^ L (LongidT v)
  else if not sml && is_ocaml_infix v then
    L LparT ^^ L (LongidT v) ^^ L RparT
  else
    L (LongidT v)

let rec
pat_to_tok_tree sml (Pvar v) = var_to_tok_tree sml v
and
pat_to_tok_tree sml (Plit l) = lit_to_tok_tree sml l
and
pat_to_tok_tree sml (Pcon c []) = var_to_tok_tree sml c
and
pat_to_tok_tree sml (Pcon c ps) =
  L LparT ^^ var_to_tok_tree sml c ^^ 
    L LparT ^^ join_trees (L CommaT) (List.map (pat_to_tok_tree sml) ps) ^^
    L RparT ^^ L RparT

let inc_indent i = 
  if (i:num) < 30 then
    i + 2
  else
    i

let newline indent = 
  L NewlineT ^^ L (WhitespaceT indent)

let rec
exp_to_tok_tree sml indent (Raise r) =
  if sml then
    L LparT ^^ L (LongidT "raise") ^^ L (LongidT "Bind") ^^ L RparT
  else
    L LparT ^^ L (LongidT "raise") ^^ 
      L LparT ^^ L (LongidT "Match_failure") ^^ 
        L LparT ^^ L (LongidT "string_of_bool") ^^ L (LongidT "true") ^^ 
        L CommaT ^^
        L (IntT (i 0)) ^^ L CommaT ^^ L (IntT (i 0)) ^^ L RparT ^^
      L RparT ^^
    L RparT
and
exp_to_tok_tree sml indent (Lit l) =
  lit_to_tok_tree sml l
and
exp_to_tok_tree sml indent (Con c []) =
  var_to_tok_tree sml c
and
exp_to_tok_tree sml indent (Con c es) =
  L LparT ^^
  var_to_tok_tree sml c ^^ 
  L LparT ^^
  join_trees (L CommaT) (List.map (exp_to_tok_tree sml indent) es) ^^ 
  L RparT ^^ L RparT
and
exp_to_tok_tree sml indent (Var v) =
  var_to_tok_tree sml v
and
exp_to_tok_tree sml indent (Fun v e) =
  newline indent ^^
  L LparT ^^
  (if sml then L FnT else L FunT) ^^
  var_to_tok_tree sml v ^^ 
  (if sml then L DarrowT else L ArrowT) ^^ 
  exp_to_tok_tree sml (inc_indent indent) e ^^ 
  L RparT
and
exp_to_tok_tree sml indent (App Opapp e1 e2) =
  L LparT ^^
  exp_to_tok_tree sml indent e1 ^^ 
  exp_to_tok_tree sml indent e2 ^^ 
  L RparT
and
exp_to_tok_tree sml indent (App Equality e1 e2) =
  L LparT ^^
  exp_to_tok_tree sml indent e1 ^^ 
  L EqualsT ^^ 
  exp_to_tok_tree sml indent e2 ^^ 
  L RparT
and
exp_to_tok_tree sml indent (App (Opn o) e1 e2) =
  let s = match o with
    | Plus -> "+"
    | Minus -> "-"
    | Times -> "*"
    | Divide -> if sml then "div" else "/"
    | Modulo -> "mod"
  end
  in
    L LparT ^^
    exp_to_tok_tree sml indent e1 ^^ 
    L (LongidT s) ^^ 
    exp_to_tok_tree sml indent e2 ^^ 
    L RparT
and
exp_to_tok_tree sml indent (App (Opb o') e1 e2) =
  let s = match o' with
    | Lt -> "<"
    | Gt -> ">"
    | Leq -> "<="
    | Geq -> ">"
  end
  in
    L LparT ^^
    exp_to_tok_tree sml indent e1 ^^ 
    L (LongidT s) ^^ 
    exp_to_tok_tree sml indent e2 ^^ 
    L RparT
and
exp_to_tok_tree sml indent (Log lop e1 e2) =
  L LparT ^^
  exp_to_tok_tree sml indent e1 ^^ 
  (if lop = And then 
     if sml then L AndalsoT else L AmpampT
   else 
     if sml then L OrelseT else L BarbarT) ^^
  exp_to_tok_tree sml indent e2 ^^ 
  L RparT
and
exp_to_tok_tree sml indent (If e1 e2 e3) =
  newline indent ^^
  L LparT ^^
  L IfT ^^
  exp_to_tok_tree sml indent e1 ^^ 
  newline indent ^^
  L ThenT ^^
  exp_to_tok_tree sml (inc_indent indent) e2 ^^
  newline indent ^^
  L ElseT ^^
  exp_to_tok_tree sml (inc_indent indent) e3 ^^ 
  L RparT
and
exp_to_tok_tree sml indent (Mat e pes) =
  newline indent ^^
  L LparT ^^
  (if sml then L CaseT else L MatchT) ^^ 
  exp_to_tok_tree sml indent e ^^ 
  (if sml then L OfT else L WithT) ^^
  newline (inc_indent (inc_indent indent)) ^^
  join_trees (newline (inc_indent indent) ^^ L BarT) 
               (List.map (pat_exp_to_tok_tree sml (inc_indent indent)) pes) ^^ 
  L RparT
and
exp_to_tok_tree sml indent (Let v e1 e2) =
  newline indent ^^
  (if sml then L LetT ^^ L ValT else L LparT ^^ L LetT) ^^ 
  var_to_tok_tree sml v ^^ 
  L EqualsT ^^
  exp_to_tok_tree sml indent e1 ^^ 
  newline indent ^^
  L InT ^^
  exp_to_tok_tree sml (inc_indent indent) e2 ^^ 
  (if sml then newline indent ^^ L EndT else L RparT)
and
exp_to_tok_tree sml indent (Letrec funs e) =
  newline indent ^^
  (if sml then L LetT ^^ L FunT else L LparT ^^ L RecT) ^^ 
  join_trees (newline indent ^^ L AndT) 
               (List.map (fun_to_tok_tree sml indent) funs) ^^ 
  newline indent ^^
  L InT ^^
  exp_to_tok_tree sml indent e ^^ 
  (if sml then newline indent ^^ L EndT else L RparT)
and
pat_exp_to_tok_tree sml indent (p,e) =
  pat_to_tok_tree sml p ^^ 
  (if sml then L DarrowT else L ArrowT) ^^
  exp_to_tok_tree sml (inc_indent (inc_indent indent)) e
and
fun_to_tok_tree sml indent (v1,v2,e) =
  var_to_tok_tree sml v1 ^^
  var_to_tok_tree sml v2 ^^ 
  L EqualsT ^^
  exp_to_tok_tree sml (inc_indent indent) e

let rec
type_to_tok_tree (Tvar tn) =
  L (TyvarT tn)
and
type_to_tok_tree (Tapp ts tn) =
  if ts = [] then
    L (LongidT tn)
  else
    L LparT ^^
    join_trees (L CommaT) (List.map type_to_tok_tree ts) ^^ L RparT ^^ 
    L (LongidT tn)
and
type_to_tok_tree (Tfn t1 t2) =
  L LparT ^^ type_to_tok_tree t1 ^^ L ArrowT ^^ type_to_tok_tree t2 ^^ 
  L RparT
and
type_to_tok_tree Tnum =
  L (LongidT "int")
and
type_to_tok_tree Tbool =
  L (LongidT "bool")

let variant_to_tok_tree sml (c,ts) =
  if ts = [] then
    var_to_tok_tree sml c 
  else
    var_to_tok_tree sml c ^^ L OfT ^^ 
    join_trees (L StarT) (List.map type_to_tok_tree ts)

(*val typedef_to_tok_tree : bool -> num -> tvarN list * typeN * (conN * t list) list -> token tree*)
val typedef_to_tok_tree : bool -> num -> list tvarN * typeN * list (conN * list t) -> tok_tree
let typedef_to_tok_tree sml indent (tvs, name, variants) =
  (if tvs = [] then 
     L (LongidT name)
   else 
     L LparT ^^ 
     join_trees (L CommaT) (List.map (fun tv -> L (TyvarT tv)) tvs) ^^ 
     L RparT ^^
     L (LongidT name)) ^^ 
  L EqualsT ^^
  newline (inc_indent (inc_indent indent)) ^^
  join_trees (newline (inc_indent indent) ^^ L BarT) 
               (List.map (variant_to_tok_tree sml) variants)

let rec 
dec_to_tok_tree sml indent (Dlet p e) =
  (if sml then L ValT else L LetT) ^^
  pat_to_tok_tree sml p ^^ 
  L EqualsT ^^
  exp_to_tok_tree sml (inc_indent indent) e ^^
  (if sml then L SemicolonT else L SemisemiT)
and
dec_to_tok_tree sml indent (Dletrec funs) =
  (if sml then L FunT else L LetT ^^ L RecT) ^^ 
  join_trees (newline indent ^^ L AndT) 
             (List.map (fun_to_tok_tree sml indent) funs) ^^
  (if sml then L SemicolonT else L SemisemiT)
and
dec_to_tok_tree sml indent (Dtype types) =
  (if sml then L DatatypeT else L TypeT) ^^ 
  join_trees (newline indent ^^ L AndT) 
             (List.map (typedef_to_tok_tree sml indent) types) ^^
  (if sml then L SemicolonT else L SemisemiT)

let dec_to_sml_string d = 
  tok_list_to_string true (tree_to_list (dec_to_tok_tree true 0 d) [])
let dec_to_ocaml_string d = 
  tok_list_to_string false (tree_to_list (dec_to_tok_tree false 0 d) [])
